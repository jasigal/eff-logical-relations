open import CBPV.Syntax renaming ( _,_ to _,,_ )
open import CBPV.BigStep

open import Data.Product
open import Data.Unit
open import Relation.Binary.PropositionalEquality

module CBPV.LogicalRelation where

empty : Env âˆ…
empty = Î» ()

â‡“v-well-defined : âˆ… âŠ¢v A â†’ Set
â‡“v-well-defined V = âˆƒ[ W ] empty âŠ¢v V â‡“ W

â‡“c-well-defined : âˆ… âŠ¢c C â†’ Set
â‡“c-well-defined M = âˆƒ[ T ] empty âŠ¢c M â‡“ T

mutual
  ğ“¥âŸ¦_âŸ§ : âˆ€ (A : ValType) â†’ ClosedVal A â†’ Set
  ğ“¥âŸ¦ `âŠ¤ âŸ§ `tt = âŠ¤
  ğ“¥âŸ¦ ğ‘¼ C âŸ§ [ï½› M ï½â¨¾ Î³ ] = ğ“œâŸ¦ C âŸ§ M Î³
  ğ“¥âŸ¦ Aâ‚ `Ã— Aâ‚‚ âŸ§ `âŸ¨ Wâ‚ , Wâ‚‚ âŸ© = ğ“¥âŸ¦ Aâ‚ âŸ§ Wâ‚ Ã— ğ“¥âŸ¦ Aâ‚‚ âŸ§ Wâ‚‚
  ğ“¥âŸ¦ Aâ‚ `âŠ Aâ‚‚ âŸ§ (`injâ‚ W) = ğ“¥âŸ¦ Aâ‚ âŸ§ W
  ğ“¥âŸ¦ Aâ‚ `âŠ Aâ‚‚ âŸ§ (`injâ‚‚ W) = ğ“¥âŸ¦ Aâ‚‚ âŸ§ W

  ğ“’âŸ¦_âŸ§ : âˆ€ (C : CompType) â†’ ClosedTerminal C â†’ Set
  ğ“’âŸ¦ A â‡’ C âŸ§ [Æ› M â¨¾ Î³ ] = âˆ€ (W : ClosedVal A) â†’ ğ“¥âŸ¦ A âŸ§ W â†’ ğ“œâŸ¦ C âŸ§ M (Î³ ++ W)
  ğ“’âŸ¦ ğ‘­ A âŸ§ (return W) = ğ“¥âŸ¦ A âŸ§ W
  ğ“’âŸ¦ Câ‚ & Câ‚‚ âŸ§ [Æ›âŸ¨ Mâ‚ , Mâ‚‚ âŸ©â¨¾ Î³ ] = ğ“œâŸ¦ Câ‚ âŸ§ Mâ‚ Î³ Ã— ğ“œâŸ¦ Câ‚‚ âŸ§ Mâ‚‚ Î³
  
  ğ“œâŸ¦_âŸ§ : âˆ€ (C : CompType) â†’ Î“ âŠ¢c C â†’ Env Î“ â†’ Set
  ğ“œâŸ¦ C âŸ§ M Î³ = âˆƒ[ T ] Î³ âŠ¢c M â‡“ T Ã— ğ“’âŸ¦ C âŸ§ T

_âŠ¨_ : âˆ€ (Î“ : Context) â†’ Env Î“ â†’ Set
Î“ âŠ¨ Î³ = âˆ€ {A : ValType} â†’ (x : Î“ âˆ‹ A) â†’ ğ“¥âŸ¦ A âŸ§ (Î³ x)

infix 4 _âŠ¨_

_^_ : âˆ€ {Î³ : Env Î“} {W : ClosedVal A}
  â†’ Î“ âŠ¨ Î³ â†’ ğ“¥âŸ¦ A âŸ§ W â†’ Î“ ,, A âŠ¨ Î³ ++ W
(Î“âŠ¨Î³ ^ W) Z = W
(Î“âŠ¨Î³ ^ W) (S x) = Î“âŠ¨Î³ x

infixl 5 _^_

val-semantic-typing : Î“ âŠ¢v A â†’ Set
val-semantic-typing {Î“} {A} V =
  âˆ€ {Î³ : Env Î“} â†’ Î“ âŠ¨ Î³ â†’ âˆƒ[ W ] Î³ âŠ¢v V â‡“ W Ã— ğ“¥âŸ¦ A âŸ§ W

comp-semantic-typing : Î“ âŠ¢c C â†’ Set
comp-semantic-typing {Î“} {C} M =
  âˆ€ {Î³ : Env Î“} â†’ Î“ âŠ¨ Î³ â†’ ğ“œâŸ¦ C âŸ§ M Î³

syntax val-semantic-typing {Î“} {A} V = Î“ âŠ¨v V âˆ· A
syntax comp-semantic-typing {Î“} {C} M = Î“ âŠ¨c M âˆ· C

mutual
  val-fundamental : âˆ€ (V : Î“ âŠ¢v A) â†’ Î“ âŠ¨v V âˆ· A
  val-fundamental (` x) {Î³} Î“âŠ¨Î³ = Î³ x , â‡“v-var refl , Î“âŠ¨Î³ x
  val-fundamental `tt Î“âŠ¨Î³ = `tt , â‡“v-tt , tt
  val-fundamental ï½› M ï½ {Î³} Î“âŠ¨Î³ with comp-fundamental M Î“âŠ¨Î³
  ... | T , Mâ‡“ , ğ“’T = [ï½› M ï½â¨¾ Î³ ] , â‡“v-thunk , T , Mâ‡“ , ğ“’T
  val-fundamental `âŸ¨ Vâ‚ , Vâ‚‚ âŸ© Î“âŠ¨Î³ with val-fundamental Vâ‚ Î“âŠ¨Î³ | val-fundamental Vâ‚‚ Î“âŠ¨Î³
  ... | Wâ‚ , Vâ‚â‡“ , ğ“¥Wâ‚ | Wâ‚‚ , Vâ‚‚â‡“ , ğ“¥Wâ‚‚ = `âŸ¨ Wâ‚ , Wâ‚‚ âŸ© , â‡“v-pair Vâ‚â‡“ Vâ‚‚â‡“ , ğ“¥Wâ‚ , ğ“¥Wâ‚‚
  val-fundamental (`injâ‚ V) Î“âŠ¨Î³ with val-fundamental V Î“âŠ¨Î³
  ... | W , Vâ‡“ , ğ“¥W = `injâ‚ W , â‡“v-injâ‚ Vâ‡“ , ğ“¥W
  val-fundamental (`injâ‚‚ V) Î“âŠ¨Î³ with val-fundamental V Î“âŠ¨Î³
  ... | W , Vâ‡“ , ğ“¥W = `injâ‚‚ W , â‡“v-injâ‚‚ Vâ‡“ , ğ“¥W

  comp-fundamental : âˆ€ (M : Î“ âŠ¢c C) â†’ Î“ âŠ¨c M âˆ· C
  comp-fundamental (Æ› M) {Î³} Î“âŠ¨Î³ =
    [Æ› M â¨¾ Î³ ] , â‡“c-abs , Î» W ğ“¥W â†’ comp-fundamental M (Î“âŠ¨Î³ ^ ğ“¥W)
  comp-fundamental (M Â· V) Î“âŠ¨Î³
      with comp-fundamental M Î“âŠ¨Î³
  ... | [Æ› L â¨¾ Î´ ] , Mâ‡“ , ğ“’LÎ´
      with val-fundamental V Î“âŠ¨Î³
  ... | W , Vâ‡“ , ğ“¥W =
    let
      T , Lâ‡“ , ğ“’T = ğ“’LÎ´ W ğ“¥W
    in T , â‡“c-app Mâ‡“ Vâ‡“ Lâ‡“ , ğ“’T
  comp-fundamental (V !) Î“âŠ¨Î³  with val-fundamental V Î“âŠ¨Î³
  ... | [ï½› M ï½â¨¾ Î´ ] , Vâ‡“ , T , Mâ‡“ , ğ“’T = T , â‡“c-force Vâ‡“ Mâ‡“ , ğ“’T
  comp-fundamental (V â¨¾ M) Î“âŠ¨Î³ with val-fundamental V Î“âŠ¨Î³ | comp-fundamental M Î“âŠ¨Î³
  ... | `tt , Vâ‡“ , tt | T , Mâ‡“ , ğ“’T = T , â‡“c-seq Vâ‡“ Mâ‡“ , ğ“’T
  comp-fundamental Æ›âŸ¨ Mâ‚ , Mâ‚‚ âŸ© {Î³} Î“âŠ¨Î³ =
    [Æ›âŸ¨ Mâ‚ , Mâ‚‚ âŸ©â¨¾ Î³ ] , â‡“c-pair , comp-fundamental Mâ‚ Î“âŠ¨Î³ , comp-fundamental Mâ‚‚ Î“âŠ¨Î³
  comp-fundamental (`projâ‚ M) Î“âŠ¨Î³ with comp-fundamental M Î“âŠ¨Î³
  ... | [Æ›âŸ¨ Mâ‚ , _ âŸ©â¨¾ Î´ ] , Mâ‡“ , (T , Mâ‚â‡“ , ğ“’T) , _ = T , â‡“c-projâ‚ Mâ‡“ Mâ‚â‡“ , ğ“’T
  comp-fundamental (`projâ‚‚ M) Î“âŠ¨Î³ with comp-fundamental M Î“âŠ¨Î³
  ... | [Æ›âŸ¨ _ , Mâ‚‚ âŸ©â¨¾ Î´ ] , Mâ‡“ , _ , (T , Mâ‚‚â‡“ , ğ“’T) = T , â‡“c-projâ‚‚ Mâ‡“ Mâ‚‚â‡“ , ğ“’T
  comp-fundamental (return V) Î“âŠ¨Î³  with val-fundamental V Î“âŠ¨Î³
  ... | W , Vâ‡“ , ğ“¥W = return W , â‡“c-return Vâ‡“ , ğ“¥W
  comp-fundamental (`let V M) Î“âŠ¨Î³
      with val-fundamental V Î“âŠ¨Î³
  ... | W , Vâ‡“ , ğ“¥W
      with comp-fundamental M (Î“âŠ¨Î³ ^ ğ“¥W)
  ... | T , Mâ‡“ , ğ“’T = T , â‡“c-let Vâ‡“ Mâ‡“ , ğ“’T
  comp-fundamental (M to N) Î“âŠ¨Î³
      with comp-fundamental M Î“âŠ¨Î³
  ... | return W , Mâ‡“ , ğ“¥W
      with comp-fundamental N (Î“âŠ¨Î³ ^ ğ“¥W)
  ... | T , Nâ‡“ , ğ“’T = T , â‡“c-to Mâ‡“ Nâ‡“ , ğ“’T
  comp-fundamental (caseÃ— V M) Î“âŠ¨Î³
      with val-fundamental V Î“âŠ¨Î³
  ... | `âŸ¨ Wâ‚ , Wâ‚‚ âŸ© , Vâ‡“ , ğ“¥Wâ‚ , ğ“¥Wâ‚‚
      with comp-fundamental M (Î“âŠ¨Î³ ^ ğ“¥Wâ‚ ^ ğ“¥Wâ‚‚)
  ... | T , Mâ‡“ , ğ“’T = T , â‡“c-caseÃ— Vâ‡“ Mâ‡“ , ğ“’T
  comp-fundamental (caseâŠ V Mâ‚ Mâ‚‚) Î“âŠ¨Î³ with val-fundamental V Î“âŠ¨Î³
  comp-fundamental (caseâŠ V Mâ‚ Mâ‚‚) Î“âŠ¨Î³ | `injâ‚ W , Vâ‡“ , ğ“¥W with comp-fundamental Mâ‚ (Î“âŠ¨Î³ ^ ğ“¥W)
  comp-fundamental (caseâŠ V Mâ‚ Mâ‚‚) Î“âŠ¨Î³ | `injâ‚ W , Vâ‡“ , ğ“¥W | T , Nâ‡“ , ğ“’T
    = T , â‡“c-caseâŠ-injâ‚ Vâ‡“ Nâ‡“ , ğ“’T
  comp-fundamental (caseâŠ V Mâ‚ Mâ‚‚) Î“âŠ¨Î³ | `injâ‚‚ W , Vâ‡“ , ğ“¥W with comp-fundamental Mâ‚‚ (Î“âŠ¨Î³ ^ ğ“¥W)
  comp-fundamental (caseâŠ V Mâ‚ Mâ‚‚) Î“âŠ¨Î³ | `injâ‚‚ W , Vâ‡“ , ğ“¥W | T , Nâ‡“ , ğ“’T
    = T , â‡“c-caseâŠ-injâ‚‚ Vâ‡“ Nâ‡“ , ğ“’T

â‡“v-total : âˆ€ (V : âˆ… âŠ¢v A) â†’ â‡“v-well-defined V
â‡“v-total V with val-fundamental V {Î³ = Î» ()} (Î» ())
... | W , Vâ‡“ , _ = W , Vâ‡“

â‡“c-total : âˆ€ (M : âˆ… âŠ¢c C) â†’ â‡“c-well-defined M
â‡“c-total M with comp-fundamental M {Î³ = Î» ()} (Î» ())
... | T , Mâ‡“ , _ = T , Mâ‡“
